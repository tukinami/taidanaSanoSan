#define TALK_EAT_RATIO 1

RandomTalk
{
    Surface_reset
    --
    TOSTR(EVAL(CHR(0x22) + RandomTalkEx() + CHR(0x22)))
}

// ------------------------------------------------------------------------------
// ランダムトーク本体：この中にランダムトークを記載してください
// ここのみ常に ' ' で括って書いてください。
// 上のRandomTalkの中身で%( )を展開していますので、" "を使わなくてもOKです。
// 高速化対策
// ------------------------------------------------------------------------------
RandomTalkEx : nonoverlap
{
    if lastTalk_flag != '' {
        lastTalk_flag = 0
    }

    _hunger_flag = On_Status.fullness.p0.judge

    // 空腹が酷い場合は有無を言わさず食事
    if _hunger_flag < -2 {
        parallel Talk_eat
        return
    }

    _talk_normal_size = RandomTalk_NormalSize
    _talk_flag = RAND(_talk_normal_size + TALK_EAT_RATIO)

    case _talk_flag {
    when 0-TALK_EAT_RATIO {
        parallel Talk_eat
    }
    others {
        RandomTalk_Normal
    }
    }
}

RandomTalk_Normal : nonoverlap
{
    parallel RandomTalk_NormalBody
}

RandomTalk_NormalSize
{
    ARRAYSIZE(RandomTalk_NormalBody)
}

RandomTalk_NormalBody : array
{
    _talk_array = IARRAY

    // Plugin GetWIndowTitle対応
    if (AYATEMPLATE.IsPluginExist('GetWindowTitle') == 1) && (Plugin_should_GetWindowTitle == 1) {
        _talk_array = (_talk_array, '\![raise,On_Plugin_GetWindowTitle]')
    }

    // 日付トーク
    _season = Time_month_toSeason
    if ISFUNC("Talk_season_%(_season)") {
        _talk_array ,= EVAL("Talk_season_%(_season)")
    }
    if ISFUNC("Talk_month_%(month)") {
        _talk_array ,= EVAL("Talk_month_%(month)")
    }
    if ISFUNC("Talk_date_%(month)_%(day)") {
        _talk_array ,= EVAL("Talk_date_%(month)_%(day)")
    }

    // 時間帯トーク
    _timeSlot = Time_hour_toSlot
    if ISFUNC("Talk_timeSlot_%(_timeSlot)") {
        _talk_array ,= EVAL("Talk_timeSlot_%(_timeSlot)")
    }

    // 通常トーク
    _talk_array ,= Talk_default
    // ワード使用トーク。これだけ配列ではない。
    _talk_array = (_talk_array, Talk_usingWord)
    // チェイントーク
    _talk_array ,= Talk_chain

    _talk_array
}
