// ===================================================================
// SwissArmyKnife (十徳ナイフ)
// ===================================================================
OnSwissArmyKnifeMenu
{
    if _argc > 0 && _argv[0] == 'noplugin' {
        '\h\s[4]十徳ナイフプラグインは無いようだ。\n\e'
        return
    }

    '\h\s[0]十徳ナイフプラグインのメニューだ。\n\n'
    --
    '\_q'
    --
    SHIORI3FW.RegisterTempVar('samenu_text', 'samenu_id')

    samenu_text = IARRAY
    samenu_id = IARRAY

    _s = ''
    _data = IARRAY

    foreach reference; _ref {
        _data = SPLIT(_ref, ',', 2)
        _id = SPLIT(_data[1], ',')

        samenu_text ,= _data[0]
        samenu_id ,= _id[0]

        if _data[1] != 'cancel' {
            _s += FillLine_list_full(_data[0], _data[1])
        }
    }
    SHIORI3FW.EscapeDangerousTags(_s)
    --
    '\n'
    --
    if call_from_menu > 0 {
        FillLine_list_full('メインメニュー', 'On_Menu_0')
    }
    --
    FillLine_list_full('閉じる', 'CLOSE_NOTALK')
    --
    '\_q'
    call_from_menu = 0
}

OnSwissArmyKnifeExecute
{
    case reference[0] {
    when 'empty_recycle_bin' {
        if TOINT(reference[1]) > 0 {
            On_Plugin.SWISSARMY.empty_recycle_bin.exist(TOINT(reference[1]), TOINT(reference[2]))
        }
        else {
            '\h\s[6]ごみ箱には何もなかった。\n'
        }
    }
    when 'delete_tmpfile' {
        _num = TOINT(reference[1])
        On_Status.pride.p0.increase(1)
        '\h\s[0]一時ファイルを削除した。\w9\n'
        --
        "%(_num)個のファイルが存在していた。\n"
    }
    }
    --
    '\n'
    --
    FillLine_list_half('メインメニュー', 'On_Menu_0', 'プラグインメニュー', 'On_Menu_0_plugin')
    --
    FillLine_list_full('閉じる', 'CLOSE_NOTALK')
}

On_Plugin.SWISSARMY.empty_recycle_bin.exist
{
    _num = TOINT(_argv[0])
    _kb = TOINT(_argv[1])
    On_Status.pride.p0.increase(1)

    {
        "\u\s[10]\h\s[6]ごみ箱を空にした。\w9\n\s[0]%(_num)個のファイル、%(_kb) KB を削除した。"
        --
        '\w9\w9\u空にしたって、\w5一体どうしたの？\w9\w9\h\n\n大きなごみ箱に移したんだよ。\w9\s[6]\nごみの回収は、\w5ここでは毎週月木金曜日。\w9\s[0]\nその時まとめて捨てる。\w9\w9\u\n\nなるほど。'
        '\w9\w9\u\s[10]スッキリ。'
        '\w9\w9\u\s[10]面倒でも不燃物とかの分別はしっかりしなさいよ。\w9\w9\h\s[7]\n\nデータにそれはないだろ！'
        if _kb > 51200 {
            // 50MB越え
            '\w9\s[4]\n…\w5…\w5。\w9\w9\uどうしたの？\w9\w9\h\s[3]\n\nちょっと、\w5重くて腰が…\w5…\w5。\w9\w9\u\s[11]\n\n日頃の運動不足が…\w5…\w5！'
        }
    }
    {
        "\h\s[0]\u\s[11]私の妖術で%(username)のごみは跡形もなく消し飛んだわ！"
        --
        '\w9\w9\h\s[3]消し…\w5…\w5…\w5\s[2]え？'
        '\w9\w9\h\s[3]パソコンってごみ箱空にしても、\w5色々すると復元できるって、\w5姉が言ってたけど。\w9\w9\u\s[10]\n\n細かいことは気にしない。'
    }
}

// ===================================================================
// スタンプ帳
// ===================================================================
On_Plugin_stamp_begin
{
    SHIORI3FW.RegisterTempVar('stamp_info_is_exist')

    "\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampInfoCall]"
    --
    "\![raise,On_Plugin_stamp_menu]"
}

OnStampInfo : void
{
    stamp_info_is_exist = TOINT(reference[1])
}

On_Plugin_stamp_menu
{
    if stamp_info_is_exist == 0 {
        FillLine_list_full("スタンプを押す", "On_Plugin_stamp_push")
    }
    --
    FillLine_list_full("スタンプメニュー", "On_Plugin_stamp_menu_exec")
    --
    '\n'
    --
    FillLine_list_half('メインメニュー', 'On_Menu_0', 'プラグインメニュー', 'On_Menu_0_plugin')
    --
    FillLine_list_full('閉じる', 'CLOSE_NOTALK')
}

On_Plugin_stamp_push
{
    '\u\s[10]\h\s[3]ん～スタンプか、\w5何を送ろうか…\w5…\w5。\w9\s[5]\nあ、\w5これなんか良いんじゃないか？\w9\n前に作った肉じゃがの画像！\w9\w9\uスタンプ違いな気がするけど、\w5まぁ、\w5いいでしょう。\w9\w9\h\n\nはい、\w5どうぞ。\w9\w9'
    --
    "\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
    ghost/master/stamp.png,/
    伊織の肉じゃがスタンプ,/
    伊織が調理した肉じゃがを写したスタンプ。おいしそうにできている。/
    ]"
    --
    "\n\n"
    --
    FillLine_list_full("スタンプメニュー", "On_Plugin_stamp_menu_exec")
    --
    FillLine_list_half('メインメニュー', 'On_Menu_0', 'プラグインメニュー', 'On_Menu_0_plugin')
    --
    FillLine_list_full('閉じる', 'CLOSE_NOTALK')
}

On_Plugin_stamp_menu_exec
{
    '\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnMenuExec]'
}

// ===================================================================
// GetWindowTitle
// ===================================================================
On_Plugin_GetWindowTitle
{
    if lastTalk_flag == 1 && getWindowTitle_lastTalk != '' {
        getWindowTitle_lastTalk
    }
    else {
        '\![raiseplugin,GetWindowTitle,OnGetWindowTitle]'
    }
}

Plugin_should_GetWindowTitle
{
    if plugin_should_GetWindowTitle == '' {
        plugin_should_GetWindowTitle = 0
    }

    plugin_should_GetWindowTitle
}

Plugin_set_should_GetWindowTitle : void
{
    _flag = TOINT(_argv[0])

    if _flag == 0 {
        plugin_should_GetWindowTitle = 0
    }
    else {
        plugin_should_GetWindowTitle = 1
    }
}

OnRecieveGetWindowTitle
{
    SHIORI3FW.RegisterTempVar('getWindowTitle_lastTalk')
    getWindowTitle_lastTalk = OnRecieveGetWindowTitle_talk
    getWindowTitle_lastTalk
}

OnRecieveGetWindowTitle_talk
{
    _window_title = SHIORI3FW.EscapeAllTags(reference[0])

    // small category
    if RE_SEARCH(_window_title, 'クックパッド') == 1 {
        _flag_max = 2
        _flag = RAND(_flag_max)

        case _flag {
        when 0 {
            // 実際早くおなかを減らす
            On_Status.fullness.p0.decrease
            --
            '\u\s[10]\h\s[8]レシピの検索をしていると腹が減って困る。\w9\w9\uいつも腹ペコでしょ貴方。\w9\w9\h\s[3]\n\nそれよりもっと。\w9\n(…\w5…\w5グ\w2ゥ\w2～\w2)\w9\w9\u\s[11]\n\n本当に！？'
        }
        others {
            '\u\s[10]\h\s[0]人のレシピを見ると、\w5結構勉強になるよな。\w9\s[6]\n使ったことなかったり、\w5見たこともない食材とか、\w5たまに見かけるし。\w9\w9\uだがしかし？\w9\w9\h\s[3]\n\n買いに行くのは面倒。\w9\w9\u\n\nやはりな。'
        }
        }
    }
    elseif (RE_SEARCH(_window_title, 'GitHub') == 1) || (RE_SEARCH(_window_title, 'GitLab') == 1) {
        '\u\s[10]\h\s[0]ぎっとレポジトリ？\w9姉がよく話してたな。\w9\s[3]\nいや、\w5「叫んでた」な。\w9\w9\uえ、\w5なんて？\w9\w9\h\s[7]\n\n「イシューが！\w9イシューがぁああ！！」\w9\s[0]\nって。\w9何のことだ？\w9\w9\u\n\nま、\w5まあ知らないでもいいことはあるでしょ。\w9\w9\h\n\n？\w9そうか？'
    }
    // large category
    elseif (RE_SEARCH(_window_title, 'wiki') == 1) || (RE_SEARCH(_window_title, 'Wiki') == 1) {
        '\u\s[10]\h\s[8]Wiki系のサイトって、\w5リンクをたどるのにハマると時間が溶けるよな。\w9\w9\u「ウィキ」、\w5「データベース」、\w5「図書館」、\w9\w9\h\s[6]\n\n「アーカイブ」、\w5「バックアップ」、\w5「ソフトウェア」、\w9\w9\u\n\n「プログラミング言語」、\w5「ページ記述言語」、\w5「マークアップ言語」、\w9\w9\h\s[0]\n\n「軽量マークアップ言語」、\w5そして「ウィキ」…\w5…\w5。\w9\w9\u\s[11]\n\n無限ループって、\w9\w9\h\s[2]\n\n怖くね？'
        '\u\s[10]\h\s[8]Wiki系のサイトって、\w5リンクをたどるのにハマると時間が溶けるよな。\w9\w9\u「ウィキ」、\w5「ハワイ語」、\w5「パイナップル」、\w9\w9\h\s[6]\n\n「螺旋」、\w5「ばね」、\w5「洗濯ばさみ」、\w9\w9\u\n\n「C47」、\w5「NGC 6934」、\w5「Milky Way」！\w9\w9\h\s[2]\n\n「ウィキ」\w5が「銀河系」\w5に！\w9\w9\u\n\nもう戻ってこれないわね。\w9\w9\h\s[3]\n\n嗚呼、\w5時間が過ぎていく…\w5…\w5。'
    }
    elseif (RE_SEARCH(_window_title, 'YouTube') == 1) || (RE_SEARCH(_window_title, '動画') == 1) {
        '\u\s[10]\h\s[0]料理作ってる動画が好きだな。\w9\w9\uそして、\w5見てると、\w9\w9\h\s[4]\n\nお\w2な\w2か\w2が\w2へ\w2る\w2…\w5…\w5。\w9\w9\u\n\nいつもの。'
    }
    elseif (RE_SEARCH(_window_title, 'FANZA') == 1) || (RE_SEARCH(_window_title, 'アダルト') == 1) {
        '\u\s[10]\h\s[9]…\w5…\w5！！\w9\s[-1]\nふぁああああ！！\w9\w9\uあっ、\w5逃げた。'
        '\u\s[10]\h\s[6]…\w5…\w5。\w9\w9\uどうしたの？\w9\w9\h\s[34]\n\nお、\w5俺はなんにも見てないから！\w9\n見てないから大丈夫！！\w9\w9\u\s[16]\n\nまぁ、\w5はい。'
    }
    elseif RE_SEARCH(_window_title, '佐野伊織') == 1 {
        "\u\s[10]\h\s[0]…\w5…\w5\w9\s[1]\n…\w5…\w5\w9\w9\u何？\w9\w9\h\n\n%(username)と目が合った気がして…\w5…\w5。"
        --
        if boot_times > 20 {
            "\w9\w9\u\n\n今更。\w9\w9\h\s[4]\n\nそういうなって。"
        }
        '\w9\w9\u\n\n見つめあっても素直におしゃべりするのよ。\w9\w9\h\s[0]\n\n？\w9あ、\w5ああ。'
        "\w9\w9\u\s[11]\n\nあ\w2～\w2な\w2た\w2～\w2は\w2だ\w2～\w2ん\w2だ\w2～\w2ん\w2ね\w2～\w2む\w2く\w2～\w2な\w2る\w2～\w9\w9\h\s[7]\n\n%(username)に催眠術やめろ！"
    }
    else {
        // TODO: 対応するものがなかったらTalk_defaultにする
        parallel Talk_default
    }
}

// ===================================================================
// MailBox
// ===================================================================
On_Plugin_MailBox_start
{
    plugin_MailBox_flag = 1
}

On_Plugin_MailBox_stop
{
    plugin_MailBox_flag = 0

    On_Plugin_MailBox_deleteAllMails
}

On_Plugin_MailBox_checkMails
{
    _s = ''
    _ids = IARRAY
    _funcs = GETFUNCLIST('Plugin_MailBox_Mail_body_')

    foreach _funcs; _fn {
        _mail_data = EVAL(_fn)
        if ARRAYSIZE(_mail_data) > 1 {
            _ids ,= _mail_data[0]
        }
    }
    _ids_str = JOIN(_ids, ':')
    "\![raiseplugin,MailBox,OnStatusMails,%(_ids_str)]"
}

On_Plugin_MailBox_resetMails
{
    _s = ''
    _vars = GETVARLIST('plugin_MailBox_mailStatus_')

    foreach _vars; _var {
        _mail_id = TOINT(REPLACE(_var, 'plugin_MailBox_mailStatus_', ''))
        _s += "\![notifyplugin,MailBox,OnDeleteMailNotArrive,%(_mail_id)]"
    }

    _s
}

On_Plugin_MailBox_sendMails
{
    if plugin_MailBox_flag == 0 {
        return
    }

    _s = ''
    _funcs = GETFUNCLIST('Plugin_MailBox_Mail_body_')

    foreach _funcs; _fn {
        _mail_data = EVAL(_fn)

        if ARRAYSIZE(_mail_data) > 1 {
            _mail_data_str = JOIN(_mail_data, ',')
            _s += "\![notifyplugin,MailBox,OnSendMailNotArrive,%(_mail_data_str)]"
        }
    }
    _s
}

On_Plugin_MailBox_deleteAllMails
{
    _s = ''
    _ids = IARRAY
    _vars = GETVARLIST('plugin_MailBox_mailStatus_')

    foreach _vars; _var {
        _mail_id = TOINT(REPLACE(_var, 'plugin_MailBox_mailStatus_', ''))
        _s += Plugin_MailBox_Mail_delete_fromId(_mail_id)
    }
    _s
}

Plugin_MailBox_BootScript
{
    _refs = IARRAY
    // CHR(34) -> ", CHR(39) -> '
    foreach reference; _r {
        _refs ,= CHR(34) + REPLACE(_r, CHR(34), CHR(39)) + CHR(34)
    }
    _ref = JOIN(_refs, ',')
    '\![embed,On_Plugin_MailBox_checkMails]'
    --
    '\![embed,On_Plugin_MailBox_resetMails]'
    --
    "\![embed,%(_argv[0]),%(_refs)]"
    --
    '\e'
}

Plugin_MailBox_Mail_delete_fromId
{
    _mail_id = TOINT(_argv[0])
    EVAL("plugin_MailBox_mailStatus_%(_mail_id)=0")
    "\![notifyplugin,MailBox,OnDeleteMail,%(_mail_id)]"
}

OnMailsStatus
{
    _mail_ids = SPLIT(reference[0], ':')
    _mail_statuses = SPLIT(reference[1], ':')

    _len = ARRAYSIZE(_mail_ids)
    for _i = 0; _i < _len; _i++ {
        _mail_id = TOINT(_mail_ids[_i])
        _status = TOINT(_mail_statuses[_i])
        EVAL("plugin_MailBox_mailStatus_%(_mail_id)=%(_status)")
    }
}

// -------------------------------------------------------------------
// Plugin_MailBox_Mail_body_*
// -------------------------------------------------------------------
// 条件つきで、条件に当て嵌らないときは空配列を返す
Plugin_MailBox_Mail_body_0 : array
{
    _id = 0

    // 半年後
    _date_raw = Time_after_day(GETSECCOUNT(), 180)
    _sender = '佐野 伊織'
    _title = '久し振り'
    _contents = "久し振り。元気にしてたか？\n\nこっちはいつも通りだよ。きぬも、俺も。\n長いこと%(username)に会えてないのは、ちょっと、寂しい、かな。\nもし、よかったらでいいから、会いに来てくれると、うれしい、なんて。\n……がらにもなかったかな。\nそれじゃあ、元気で。"

    _id
    _date_raw[0]
    _date_raw[1]
    _date_raw[2]
    _sender
    _title
    _contents
}

Plugin_MailBox_Mail_body_1 : array
{
    _id = 1

    _date_raw = GETTIME()
    _sender = '佐野 伊織'
    _title = '初めてのメール'
    _contents = "こんにちは、%(username)。\n\nちゃんと届いているかテスト送信してみた。\nこういうの慣れてないが、まぁ、それなりにやるよ。\n\nそれじゃあな。"

    _id
    _date_raw[0]
    _date_raw[1]
    _date_raw[2]
    _sender
    _title
    _contents
}

Plugin_MailBox_Mail_body_2 : array
{
    if plugin_MailBox_mailStatus_1 != 3 {
        return
    }
    --
    _id = 2

    _date_raw = Time_after_day(GETSECCOUNT(), 1)
    _sender = '佐野 伊織'
    _title = 'ちょっとしたこと'
    _contents = "こんにちは、%(username)。\nちょっとしたことがあったのでメールしてみた。\n\n%(username)は「アブラボウズ」って知ってるか？\n深海魚で、おいしいけれど、油がすごくて、大量に食べるとおなかを壊すやつ。\nそれが、近所の魚屋で売っていたんだ。\n買いたかったんだが、あいにく手持ちがなくて……。\n気になる……%(username)は食べたことあるか？\n\nそれじゃあな。"

    _id
    _date_raw[0]
    _date_raw[1]
    _date_raw[2]
    _sender
    _title
    _contents
}

Plugin_MailBox_Mail_body_3 : array
{
    if plugin_MailBox_mailStatus_2 != 3 {
        IARRAY
        return
    }

    _id = 3

    _date_raw = Time_after_day(GETSECCOUNT(), 1)
    _sender = '佐野 伊織'
    _title = 'ちょっとしたこと その2'
    _contents = "こんにちは、%(username)。\nまたちょっとしたことがあったのでメールしてみた。\n\n近所の商店街で買い物をしていたら、花屋さんで、小さな青い花、「わすれなぐさ」が咲いていたんだ。\n実物をまじまじ見ることがなかったから、こういう花なんだ、って思ったよ。\n聞いたら、本来は年を越せるんだけれど、日本では夏が厳しいんだって。\n……ちょっとせつないよな。\n\nそれじゃあな。"

    _id
    _date_raw[0]
    _date_raw[1]
    _date_raw[2]
    _sender
    _title
    _contents
}

Plugin_MailBox_Mail_body_4 : array
{
    if plugin_MailBox_mailStatus_3 != 3 {
        IARRAY
        return
    }

    _id = 4

    _date_raw = Time_after_day(GETSECCOUNT(), 1)
    _sender = 'きぬ'
    _title = 'こんにちは'
    _contents = "こんにちは、%(username)。きぬよ。\n伊織に代わりに入力してもらっているわ。\nたまには伊織もすなるメールというものを、私もしてみんとて、と思ってね。\n\nまあ私も何があったわけじゃないんだけど。\n%(username)が元気なら良いわ。\n\nじゃあ、またね！"

    _id
    _date_raw[0]
    _date_raw[1]
    _date_raw[2]
    _sender
    _title
    _contents
}

// ===================================================================
// Inventory
// ===================================================================
#define INVENTORY_ITEM_COUNT_PREFIX_ plugin_inventory_count_item_
#define INVENTORY_RECIPE_FUNC_PREFIX_ Plugin_Inventory_Recipe_
#define INVENTORY_CREATE_FUNC_PREFIX_ On_Plugin_Inventory_Create_
#define INVENTORY_COMMUNICATE_FUNC_PREFIX_ On_Plugin_Inventory_Communicate_Func_
On_Plugin_Inventory_RecipeMenu_start
{
    _s = ''

    _items = Plugin_Inventory_Item_list()
    foreach _items; _item {
        _s += '\![embed,On_Plugin_Inventory_Item_check,' + _item + ']'
    }
    _recipes = GETFUNCLIST('INVENTORY_RECIPE_FUNC_PREFIX_')
    foreach _recipes; _recipe {
        _name = REPLACE(_recipe, 'INVENTORY_RECIPE_FUNC_PREFIX_', '')
        _s += '\![embed,On_Plugin_Inventory_Item_check,' + _name + ']'
    }

    _s
    --
    '\![embed,On_Plugin_Inventory_RecipeMenu_open]'
}

On_Plugin_Inventory_Item_check
{
    "\![raiseplugin,Inventory,OnCheckItem,%(reference[0])]"
}

On_Plugin_Inventory_Item_init : void
{
    SHIORI3FW.RegisterTempVar('plugin_inventory_items')
    plugin_inventory_items = IARRAY()
    _current_list = IARRAY()
    _recipes = GETFUNCLIST('INVENTORY_RECIPE_FUNC_PREFIX_')
    foreach _recipes; _recipe {
        _materials = EVAL(_recipe)
        _current_list = (_current_list, _materials)
    }
    plugin_inventory_items = ARRAYDEDUP(_current_list)
}

Plugin_Inventory_Item_list : array
{
    if plugin_inventory_items == '' {
        On_Plugin_Inventory_Item_init()
    }
    plugin_inventory_items
    // ----------------------------------------
    // 現在対応しているもの
    // ----------------------------------------
    // '缶詰'
    // 'キャベツ'
    // 'レタス'
    // 'パスタ'
    // '小麦粉'
    // '松茸'
    // 'タケノコ'
    // '猪肉'
    // 'フォアグラ'
    // 'トリュフ'
    // '茶碗蒸し'
    // '寒天（プリン味）'
    // 'プリンチョコ'

    // 'パン'
}

OnInventoryResult
{
    _name = reference[0]
    _count = TOINT(reference[1])
    _varname = 'INVENTORY_ITEM_COUNT_PREFIX_' + _name
    SHIORI3FW.RegisterTempVar(_varname)
    LETTONAME(_varname, _count)
}

On_Plugin_Inventory_RecipeMenu_open
{
    _current_list = Plugin_Inventory_RecipeMenu_list()
    if ARRAYSIZE(_current_list) > 0 {
        _header = Plugin_Inventory_RecipeMenu_header()
        _footer = Plugin_Inventory_RecipeMenu_footer()

        Paging_begin(_header, _footer, 12, 2, 0, _current_list)
    }
    else {
        '\u\s[10]\h\s[4]んー、\w5今は何も作れなさそうだ。\w9\w9\u材料が必要ね。\w9\nどこかから調達できればいいんだけど。'
        --
        '\0\n\n[half]'
        --
        Plugin_Inventory_RecipeMenu_footer()
    }
}

Plugin_Inventory_RecipeMenu_list : array
{
    _current_list = IARRAY()
    _funcs = GETFUNCLIST('INVENTORY_RECIPE_FUNC_PREFIX_')

    foreach _funcs; _func {
        _materials = EVAL(_func)
        _material_list = ''
        _is_clear = 0
        foreach _materials; _material {
            if Plugin_Inventory_item_exists(_material) {
                _is_clear++
                _material_list += ',' + _material
                continue
            }
            _is_clear = 0
            break
        }

        if _is_clear > 0 {
            _name = REPLACE(_func, 'INVENTORY_RECIPE_FUNC_PREFIX_', '')
            _select = "\q[%(_name),On_Plugin_Inventory_Result,%(_name)%(_material_list)]"
            if Plugin_Inventory_item_exists(_name) > 0 {
                _current_list ,= _select
            }
            else {
                _current_list ,= "@@font_bold" + _select + "@@font_bold_default"
            }
        }
    }

    _current_list
}

Plugin_Inventory_item_exists
{
    if ISVAR('INVENTORY_ITEM_COUNT_PREFIX_' + _argv[0]) {
        _count = EVAL('INVENTORY_ITEM_COUNT_PREFIX_' + _argv[0])
        if _count > 0 {
            1
            return
        }
    }
    0
}

Plugin_Inventory_RecipeMenu_header : nonoverlap
{
    '\u\s[10]\h\s[6]よし、\w5何か作るか。'
    '\u\s[10]\h\s[5]そうだな、\w5何か作ろう。'
    '\h\s[0]\u\s[11]何になるかな、\w5何になるかな？\w9\w9\h\s[3]作るの俺だが？'
}

Plugin_Inventory_RecipeMenu_footer
{
    FillLine_list_full('「Inventory」のメニュー', 'On_Plugin_Inventory_call_menu')
    --
    FillLine_list_half('メインメニュー', 'On_Menu_0', 'プラグインメニュー', 'On_Menu_0_plugin')
    --
    FillLine_list_full('閉じる', 'CLOSE_NOTALK')
}

On_Plugin_Inventory_call_menu
{
    '\![raiseplugin,Inventory,OnMenuExec]'
}

On_Plugin_Inventory_Result
{
    if ARRAYSIZE(reference) < 1 {
        '\h\s[0]\u\s[11]むむっ！！\w9\w9\h\s[4]…\w5…\w5何かがおかしいなようだ。\w9\n「Inventory」のときに何か起こっていると作者に伝えてくれ。'
        return
    }

    _s = "\![notifyplugin,Inventory,OnIncrementItem,%(reference[0])]"
    for _i = 1; _i < ARRAYSIZE(reference); _i++ {
        _s += "\![notifyplugin,Inventory,OnDecrementItem,%(reference[_i])]"
    }
    _s
    --
    Plugin_Inventory_Result_header_func(reference[0])
    --
    if ISFUNC('INVENTORY_CREATE_FUNC_PREFIX_' + reference[0]) {
        EVAL('INVENTORY_CREATE_FUNC_PREFIX_' + reference[0])
    }
    else {
        _name = SHIORI3FW.EscapeAllTags(reference[0])
        "\u\s[10]\h\s[6]…\w5…\w5\w9\s[7]よし！\w9%(_name)が作れたぞ！\w9\w9\uYeah！！\w9\w9\h\s[5]\n\n「Inventory」に入れておくから、\w9\n使いたい場合はプラグインを呼んでくれ。"
    }
    --
    Plugin_Inventory_Result_footer
}

Plugin_Inventory_Result_header_func
{
    "\0\_q@@font_bold" + SHIORI3FW.EscapeAllTags(_argv[0]) + "@@font_bold_default\_q\n"
}

Plugin_Inventory_Result_footer
{
    '\0\n\n[half]'
    --
    FillLine_list_full('もう一回作る', 'On_Plugin_Inventory_RecipeMenu_start')
    --
    Plugin_Inventory_RecipeMenu_footer()
}

INVENTORY_RECIPE_FUNC_PREFIX_パン : array
{
    '小麦粉'
}

INVENTORY_RECIPE_FUNC_PREFIX_キャベツとツナのパスタ : array
{
    '缶詰'
    'キャベツ'
    'パスタ'
}

INVENTORY_RECIPE_FUNC_PREFIX_タケノコと松茸の茶碗蒸し : array
{
    'タケノコ'
    '松茸'
    '茶碗蒸し'
}

INVENTORY_RECIPE_FUNC_PREFIX_フォアグラ・パテのパン : array
{
    'パン'
    'フォアグラ'
}

INVENTORY_RECIPE_FUNC_PREFIX_猪肉のステーキ（フォアグラとトリュフを添えて） : array
{
    'フォアグラ'
    'トリュフ'
    '猪肉'
}

INVENTORY_RECIPE_FUNC_PREFIX_レタスと猪肉のパスタ : array
{
    'パスタ'
    'レタス'
    '猪肉'
}

INVENTORY_RECIPE_FUNC_PREFIX_プリンアラモード？ : array
{
    '寒天（プリン味）'
    'プリンチョコ'
    '缶詰'
}

INVENTORY_RECIPE_FUNC_PREFIX_ペペロンチーノ（素） : array
{
    'パスタ'
}

INVENTORY_CREATE_FUNC_PREFIX_パン
{
    '\u\s[10]\h\s[6]小麦粉に塩とか水とか色々入れまして。\w9\w9\u色々て。\w9\w9\h\n\n寝かしたり、\w5型に入れて焼いたりしますと…\w5…\w5。\w9\s[35]\nおーし！\w9パンが焼けたぞ！\w9\w9\u\n\n時間がすっとんだ。\w9\w9\h\s[8]\n\n気にしない。'
}

INVENTORY_CREATE_FUNC_PREFIX_キャベツとツナのパスタ
{
    '\u\s[10]\h\s[6]茹でたパスタに、\w5キャベツとツナで作ったソースを絡めて…\w5…\w5と。\w9\s[5]\nできた。\w9キャベツとツナのパスタ。\w9\w9\uなんだかヘルシーじゃない。\w9\w9\h\s[35]\n\nもちろん、\w5おいしい！\w9\w9\u\n\n重要。'
}

INVENTORY_CREATE_FUNC_PREFIX_タケノコと松茸の茶碗蒸し
{
    '\u\s[10]\h\s[6]茶碗蒸しにタケノコと松茸をのせて温めなおして、\w5と。\w9\w9\u\s[11]載せただけ！？\w9\w9\h\s[3]\n\nいいじゃないか、\w5おいしいぞ。'
}

INVENTORY_CREATE_FUNC_PREFIX_フォアグラ・パテのパン
{
    '\u\s[10]\h\s[6]フォアグラで作ったパテをパンにのせて、\w5と。\w9\w9\u贅沢～。'
}

INVENTORY_CREATE_FUNC_PREFIX_猪肉のステーキ（フォアグラとトリュフを添えて）
{
    '\u\s[10]\h\s[6]今回は思いっきりぜいたくします！\w9\s[9]\nこの分厚い猪肉にフォアグラとトリュフをトッピング！\w9\w9\uおお～。\w9\w9\h\s[36]\n\nへへ、\w5とてもおいしそう。'
}

INVENTORY_CREATE_FUNC_PREFIX_レタスと猪肉のパスタ
{
    '\u\s[10]\h\s[6]お肉とレタスは相性がいいです。\w9\s[5]\nなので、\w5今回は猪肉と合わせてパスタにしました。\w9\w9\uおおー。\w9\w9\h\s[35]\n\n味は保証できます。\w9おいしい。\w9\w9\u\n\n味見済み。'
}

INVENTORY_CREATE_FUNC_PREFIX_プリンアラモード？
{
    '\u\s[10]\h\s[9]き、\w5きっとプリン味の寒天ならいけるはずだ！\w9\n缶詰のフルーツものせたし！\w9\w9\u本当に？\w9\w9\h\s[4]\n\n食えないものにはなっていない、\w5はず！'
}

INVENTORY_CREATE_FUNC_PREFIX_ペペロンチーノ（素）
{
    '\u\s[10]\h\s[6]パスタ以外何もないときの強い味方。\w9\n素のペペロンチーノ。\w9\w9\u但し、\w5油、\w5ニンニク、\w5唐辛子は家にあるものとする。\w9\w9\h\s[3]\n\n言うなよ…\w5…\w5。'
}

// -------------------------------------------------------------------
// OnCommunicate from ReplyToPlugin
Plugin_Inventory_Communicate_check_reference
{
    STRSTR(reference[1], 'use:', 0) == 0
}

Plugin_Inventory_Communicate_Main
{
    _item_name = REPLACE(reference[1], 'use:', '')
    if ISFUNC('INVENTORY_COMMUNICATE_FUNC_PREFIX_' + _item_name) {
        EVAL('INVENTORY_COMMUNICATE_FUNC_PREFIX_' + _item_name)
    }
    else {
        _name = SHIORI3FW.EscapeAllTags(_item_name)
        "\u\s[10]\h\s[0]おー、\w5%(_name)か。\w9\n\w9\w9\u私たちには使い道がないけれど、\w5他の人ならあるいは、\w5ね。"
    }
    --
    '\0\n\n[half]'
    --
    Plugin_Inventory_Communicate_footer()
}

Plugin_Inventory_Communicate_footer
{
    Plugin_Inventory_RecipeMenu_footer()
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_缶詰
{
    '\u\s[10]\h\s[2]なんだか色々な缶詰があるな。\w9\w9\uこれって、\w5料理に使える？\w9\w9\h\s[0]\n\n確かに、\w5いろいろできそうだ。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_キャベツ
{
    '\u\s[10]\h\s[6]キャベツはいい。\w9煮ても焼いても、\w5生でもうまい。\w9\w9\u料理できる？\w9\w9\h\s[5]\n\n材料さえ揃えば、\w5もちろん。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_レタス
{
    '\u\s[10]\h\s[7]レタスは生の印象が強いが、\w5火をいれてもいけるぞ！\w9\w9\u力いっぱい。\w9\w9\h\s[6]\n\nもちろん、\w5料理に使えます。\w9\w9\u\n\n急に説明的。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_パスタ
{
    '\u\s[10]\h\s[0]パスタは忙しいときの心強い味方です。\w9\w9\u忙しくなくても？\w9\w9\h\s[6]\n\n料理しておいしくいただけます。\w9\w9\u\s[16]\n\nそうなんだけどさ。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_小麦粉
{
    '\u\s[10]\h\s[0]この小麦粉はパン用っぽいな。\w9\s[6]\nパン、\w5作るか。\w9\w9\u私の手伝えるやつね。\w9\w9\h\s[3]\n\n？？\w9\w9\u\s[11]\n\n猫とは生粋のパンこね職人なのよ！\w9\w9\h\s[4]\n\n初耳だよそんなの。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_松茸
{
    '\u\s[10]\h\s[9]松茸！\w9高級食材だ！\w9\w9\u佐野家では、\w5これを湯水のように料理に使っています。\w9\w9\h\s[3]\n\n嘘をつくな、\w5嘘を。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_タケノコ
{
    '\u\s[10]\h\s[5]タケノコか。\w9おいしいよな、\w5タケノコ。\w9\s[35]\n先の方は柔らかく、\w5根本の方はコリコリ食感がいい。\w9\n料理に使いがいがある。\w9\w9\uつまり、\w5これを使って何かを？\w9\w9\h\s[0]\n\nそうだな、\w5何か作れると思う。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_猪肉
{
    '\u\s[10]\h\s[0]猪の肉は、\w5豚肉に似ている。\w9\nそして、\w5豚より味が濃い。\w9\w9\u日本でも外国でも食べられているわね。\w9\w9\h\s[5]\n\n調理に工夫は必要だが、\w5おいしい肉だからな。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_フォアグラ
{
    '\u\s[10]\h\s[9]フォ、\w5フォアグラ！？\w9高級食材だ！\w9\w9\u佐野家では、\w5これを湯水のように料理に使っています。\w9\w9\h\s[34]\n\nそんな訳ない！\w9\nほぼ口に入ったこともない！！'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_トリュフ
{
    '\u\s[10]\h\s[2]おお、\w5トリュフだ！\w9高級食材じゃないか！\w9\w9\uたまにトリュフ入りのポテトチップスとかあるわよね。\w9\w9\h\s[8]\n\nその手のものでしか食べたことないかもな。\w9\s[6]\n使うなら、\w5どんな料理ができるだろう。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_茶碗蒸し
{
    '\u\s[10]\h\s[35]茶碗蒸しか。\w9食べるとほっとできていい。\w9\w9\u具材の種類で色々楽しめたりもするわね。\w9\w9\h\s[5]\n\nなかなかアレンジしがいがあったりするな。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_寒天（プリン味）
{
    '\u\s[10]\h\s[0]プリン味の寒天か。\w9\s[4]\nこう、\w5プリンだと思わなければおいしい、\w5と思うんだが、\w5見た目がプリンなので損をしている気がするな。\w9\w9\u何かと組み合わせれば、\w5よくなるんじゃない？\w9\w9\h\s[3]\n\nそうかも。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_プリンチョコ
{
    '\u\s[10]\h\s[0]プリン味のチョコレートか。\w9\s[6]\n結構カスタードの風味がする。\w9\n俺はいけるな。\w9\w9\u何かと組み合わせればもっとよくなるかも？\w9\w9\h\s[5]\n\nそうかもな。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_パン
{
    '\u\s[10]\h\s[6]パンは単体でもおいしいが、\w5いろいろアレンジできるのもいいよな。\w9\w9\uジャムとか塗ったり？\w9\w9\h\s[5]\n\nそうだな。\w9\n肉とかのせてもおいしいし。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_キャベツとツナのパスタ
{
    '\u\s[10]\h\s[35]キャベツとツナの黄金コンビに、\w5パスタが合わさって、\w5美味！\w9\w9\uヘルシーさもあって、\w5いいわよね。\w9\w9\h\s[7]\n\nこんなに健康的な料理です！\w9\nいくら食べてもよい！\w9\w9\u\n\nそれは違う。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_タケノコと松茸の茶碗蒸し
{
    '\u\s[10]\h\s[5]やわらかい茶碗蒸しに、\w5タケノコの食感と松茸の香りが加わって、\w5いい感じだ。\w9\w9\u具が加わると、\w5華やかになるわね。\w9\w9\h\s[7]\n\n今！\w9三人の力が合わさり！\w9\n皆に幸せ運ぶ強き力が目覚める…\w5…\w5！\w9\w9\u\n\n何の話よ、\w5何の。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_フォアグラ・パテのパン
{
    '\u\s[10]\h\s[6]パンにフォアグラのパテをのせる。\w9\nこんな贅沢があっていいのだろうか。\w9\w9\uフランスではよく食べられているそうよ。\w9\w9\h\s[2]\n\nフランスすごいな！？'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_猪肉のステーキ（フォアグラとトリュフを添えて）
{
    "\u\s[10]\h\s[36]分厚い猪肉に、\w5フォアグラとトリュフを合わせる日が来るとは…\w5…\w5！\w9\w9\uすごい贅沢よね。\w9\w9\h\s[6]\n\n%(username)のなので、\w5俺は食べません。\w9\w9\u\n\nおっ。\w9\w9\h\s[3]\n\n匂いだけで、\w5我慢します。\w9\w9\u\n\nお、\w5おぅ。"
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_レタスと猪肉のパスタ
{
    '\u\s[10]\h\s[5]お肉とレタスを一緒に食べるとおいしいよな。\w9\s[35]\nそして、\w5その風味でもってパスタを食べる…\w5…\w5。\w9\s[6]\nいい…\w5…\w5。\w9\w9\u何この流れ。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_プリンアラモード？
{
    '\u\s[10]\h\s[4]うん、\w5悪くはない、\w5悪くはないんだが。\w9\w9\uこれがプリンアラモードかと言われると？\w9\w9\h\s[3]\n\nなんだろう。\w9\nやはりプリンではないから、\w5別物として食べたほうが幸せだな。'
}

INVENTORY_COMMUNICATE_FUNC_PREFIX_ペペロンチーノ（素）
{
    '\u\s[10]\h\s[35]おいしいペペロンチーノは、\w5素でもおいしい。\w9\w9\u妙なトートロジー。\w9\w9\h\s[3]\n\nとー…\w5…\w5何？\w9\w9\u\s[16]\n\nいえ、\w5なんでもないわ。'
}
