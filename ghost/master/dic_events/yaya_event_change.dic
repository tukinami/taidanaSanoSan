// ******************************************************************************
// YAYA　ゴーストテンプレート
// 切り替えトーク辞書
// ******************************************************************************

// ******************************************************************************
// ゴースト切り替え
// ******************************************************************************
/* 関数は以下の形で記載します。
[イベント] + [ゴースト名]
イベントは、以下のものがあります。
イベントによっては該当する関数が無ければOnCloseやOnBootになります。
ゴースト名は、先頭に _が付きます。
該当するゴースト名が無いときは、ゴースト名の部分は「Other」となります。

名前の中に"や'、あるいは+-/*、半角空白など、
システムで使われている文字が来る場合は、すべて_に変換されます。
例：the "MobileMaster" -> OnGhostChanging_the__MobileMaster_

OnGhostChanging		他のゴーストへ切り替えしようとしたとき。無ければOnCloseが呼ばれます。
OnGhostChanged		他のゴーストから切り替わったとき。無ければOnBootが呼ばれます。
OnGhostCalling		他のゴーストを呼び出そうとしたとき
OnGhostCalled			他のゴーストから呼び出されたとき。無ければOnBootが呼ばれます。
OnGhostCallComplete	他のゴーストを呼び出して、呼び出されたゴーストの起動が完了したとき、OnOtherGhostBootedの代わりに

OnOtherGhostBooted	他のゴーストが呼び出されて、呼び出されたゴーストの起動が完了したとき
OnOtherGhostClosed	他のゴーストが終了したとき
OnOtherGhostChanged	他のゴーストが他のゴーストに 切り替わって、切り替わり先のゴーストの起動が完了したとき */

// -------------------------------------------------------------------
// 自分へ
// -------------------------------------------------------------------
OnGhostChanged_佐野伊織
{
    '\u\s[10]\h\s[0]リロード完了。\w9\w9\uリロード中何かあった？\w9\w9'
    --
    '\h\s[6]\n\nそれは一瞬の出来事であった。\w9\n佐野伊織を襲った謎の眠気。\w9\nそれは、\w5これから始まる奇怪な事件の序章に過ぎなかったのだ…\w5…\w5。\w9\w9\u\n\n実際には？\w9\w9\h\s[8]\n\n何もなかったです。\w9はい。'
    '\h\s[5]\n\n点検・\w5確認・\w5異常なし！\w9\w9\u\n\nヨシッ！'
    '\h\s[31]\n\n一瞬ですが、\w5眠りました。\w9\w9\u\s[11]\n\n本当に！？'
    --
    '\e'
}

OnGhostChanging_佐野伊織
{
    '\h\s[0]\u\s[11]リ\w2ロ\w2ー\w2ー\w2ー\w2ー\w2ド\w2ッ！！\w9\w9\h\s[6]そこまでのことだろうか？'
    '\u\s[10]\h\s[6]リロード開始。'
    '\h\s[0]\u\s[10]リロードしまーす！'
    --
    '\e'
}

// -------------------------------------------------------------------
// ドラゴンリーダー
// -------------------------------------------------------------------
OnGhostChanged_リーダー
{
    OnBoot_talk_prelude
    --
    _temp = OnBoot_talk_priority
    if _temp != '' {
        _temp
        return
    }

    '\u\s[10]\h\s[36]おお、\w5干し肉だ！\w9ありがとう！\w9\nリーダーさんにもよろしく伝えてくれ！\w9\w9\uリーダーさんありがとうございまーす！！\w9\w9\h\s[3]\n\nちなみに、\w5何の肉だろうな。\w9\w9\u\n\nファンタジーな生き物の肉なのでは？\w9\n妙にでかいイノシシとか。\w9\w9\h\s[34]\n\n「妙にでかいイノシシ」\w5はファンタジーなのか？'
    --
    '\e'
}

OnGhostChanging_リーダー
{
    '\u\s[10]\h\s[5]リーダーさんのところに行くのか？\w9\nなら、\w5これ。\w9\nクッキー焼いたから、\w5持っていってくれ。\w9\w9\u…\w5…\w5ドラゴンサイズだから、\w9\n皿みたいになってる…\w5…\w5。\w9\w9\h\s[7]\n\n大は小を兼ねます！\w9多分！\w9\w9\u\s[16]\n\nそうかなぁ。'
    --
    '\w9\w9\e'
}

// -------------------------------------------------------------------
// 将人withアリサ
// -------------------------------------------------------------------
OnGhostChanged_将人
{
    OnBoot_talk_prelude
    --
    _temp = OnBoot_talk_priority
    if _temp != '' {
        _temp
        return
    }

    if '素敵な猫' _in_ reference[1] {
        '\h\s[0]\u\s[16]ふふ、\w5ありがとうございます。\w9\s[11]\n私のモフモフ胸毛に酔いな！\w9\w9\h\s[3]アリサさんもモフモフだろ？\w9\w9\u\n\n二人はモフモフ！\w9Maxモフ！\w9\w9\h\s[4]\n\n何を言っているんだ？'
    }
    elseif '雑談するもの同士' _in_ reference[1] {
        '\u\s[10]\h\s[4]が、\w5が、\w5が…\w5…\w5。\w9\s[9]\n頑張れるときは頑張ります！\w9\n多分、\w5おそらく！\w9\w9\uまぁ、\w5及第点でしょう。\w9\w9\h\s[6]\n\n将人さんもご無理はなさらず！\w9\n怠惰は人を助く！\w9\w9\u\s[11]\n\nえっ。'
    }
    else {
        // フォールバック
        '\u\s[10]\h\s[5]将人さんとアリサさんから交代だ。\w9\w9\uありがとうございました。'
    }
}

OnGhostChanging_将人
{
    '\u\s[10]\h\s[5]将人さんとアリサさんへ交代だ。\w9\s[6]\nということで、\w5俺はしばしお休み。\w9\w9\uいつもじゃないの。\w9何かしなさいな。\w9\w9\h\s[3]\n\nしょうがないな。\w9それじゃあ、\w9\s[6]\nご飯でも作るか。\w9\w9\u\s[16]\n\nそれもいつもの。'
    --
    '\w9\w9\e'
}

// -------------------------------------------------------------------
// ロスト・ユー・サムウェア
// -------------------------------------------------------------------
OnGhostChanged_ルストリカ
{
    OnBoot_talk_prelude
    --
    _temp = OnBoot_talk_priority
    if _temp != '' {
        _temp
        return
    }

    if '猫の妖精のようなもの' _in_ reference[1] {
        '\u\s[10]\h\s[0]ルストリカさんからの交代だ。\w9\w9\u\s[16]妖精、\w5懐かしい響きね。\w9\w9\h\n\n会ったことがあるのか？\w9\w9\u\s[10]\n\nまぁね。\w9\s[11]\n…\w5…\w5ちなみに尻尾は気が付いたらこうなっていました。\w9\nモフモフが二倍でお得感があるでしょう！？\w9\w9\h\s[3]\n\n話をそらされたような。'
    }
    elseif '便利な機械はないか' _in_ reference[1] {
        '\h\s[0]\u\s[10]ルストリカさんからの交代です。\w9\w9\h\s[6]洗濯が大体自動で終わっても、\w9\nその次には干すのが面倒になるのが人のサガ。\w9\w9\u\n\n何言ってるんだか。\w9\nそれに、\w5スープは否定しないんだ。\w9\w9\h\s[7]\n\nおいしいものはおいしい！\w9\w9\u\n\nへいへい。'

    }
    else {
        // フォールバック
        '\u\s[10]\h\s[5]ルストリカさんから交代だ。\w9\w9\uありがとうございました。'
    }
}

OnGhostChanging_ルストリカ
{
    '\u\s[10]\h\s[0]ルストリカさんのところでは、\w9\n魔法が非日常のものではないそうだ。\w9\w9\u魔法で楽しようとしないの。\w9\w9\h\s[34]\n\nまだ言ってないのに！\w9\w9\u\n\nまだって言った。'
    '\u\s[10]\h\s[6]ルストリカさんは魔法も使えるし、\w9\n一人暮らししているし、\w5すごい。\w9\w9\uどうしたの？\w9急に。\w9\w9\h\s[5]\n\nちょっと憧れがあるんだ。\w9両方に。\w9\w9\u\n\n…\w5…\w5貴方が一人暮らしを？\w9\w9\h\s[3]\n\n突っ込むのそっちか。'
    --
    '\w9\w9\e'
}

// -------------------------------------------------------------------
// 餓我 -ガガ-
// -------------------------------------------------------------------
OnGhostChanged_ミスティー
{
    OnBoot_talk_prelude
    --
    _temp = OnBoot_talk_priority
    if _temp != '' {
        _temp
        return
    }

    if 'クッキー' _in_ reference[1] {
        ghostchanged_GaGa_cookie++
        '\u\s[10]\h\s[36]わー！！\w9エメラさん、\w5クッキーありがとうございます！\w9\n大好きです！！\w9いただきます！\w9\s[6]\nもぐもぐ…\w5…\w5。\w9\s[35]\nおいしい！！\w9ごちそうさま！\w9\w9\u（餌付けされてる…\w5…\w5)\w9\w9\h\s[3]\n\nえっ、\w5何その視線。'
    }
    elseif '9時間の眠り' _in_ reference[1] {
        '\u\s[10]\h\s[30]ふ\w2ぁ\w2ぁ\w2ぁ\w2…\w5…\w5。\w9\s[31]\nミスティーさんたちからの、\w5交代です…\w5…\w5。\w9\s[25]\nよく、\w5寝…\w5…\w5。\w9\s[6]\n…\w5…\w5。\w9\w9\u\s[11]寝るな！！'
    }
    elseif '目覚まし時計' _in_ reference[1] {
        ghostchanged_GaGa_alarm++
        '\u\s[10]\h\s[6]\_q(ジリリリリリリ！）\_q\w9\s[2]\nはわわわわわわ！？\w9\w9\uミスティーさんたちからの交代よ！\w9\nさぁ、\w5シャキっとしなさいな！\w9\w9\h\s[3]\n\n起きるけどさ…\w5…\w5。'
    }
    elseif '冷凍のカニ' _in_ reference[1] {
        '\u\s[10]\h\s[7]みくびってもらっては困る！\w9\nこちらは美味しいものならなんでも喜ぶたちだぞ！\w9\s[6]\nいや、\w5ありがとうございます。\w9\n流石にカニは家族で食べます。\w9\w9\u1ヶ月はカニづくしね。\w9\w9\h\s[3]\n\nちなみに猫はカニ食べられないからな。\w9\w9\u\s[16]\n\nいやん。'
    }
    else {
        // フォールバック
        '\u\s[10]\h\s[6]ミスティーさんたちからの交代だ。\w9\w9\uありがとうございました。'
    }
}

OnGhostChanging_ミスティー
{
    _flag = RAND(2)

    if ghostchanged_GaGa_cookie > 0 {
        ghostchanged_GaGa_cookie--
        '\u\s[10]\h\s[5]エメラさんにクッキーをいただいたから、\w5お返しに。\w9\nこれ、\w5「おまんじゅう」なんだが、\w5届けてくれないか？\w9\w9\u気に入ってくれると良いわね。\w9\w9\h\s[35]\n\nそうだな。\w9\nよろしく頼む。'
    }
    elseif ghostchanged_GaGa_alarm > 0 {
        ghostchanged_GaGa_alarm--
        '\u\s[10]\h\s[6]この間の目覚まし時計のお返しをしとうございます。\w9\w9\u珍しい。\w9\w9\h\s[7]\n\nゴーストフォルダヨンさんあてにギフト爆撃だ！\w9\nお高い生菓子セット！！\w9\w9\u\s[11]\n\nはぁ？\w9\w9\h\s[8]\n\nせまる賞味期限の恐怖が彼(?)を襲うだろう。\w9\w9\u\s[10]\n\nはぁ…\w5…\w5。'
    }
    else {
        '\u\s[10]\h\s[7]ゴースト「餓我 -ガガ-」。\w9\nその素敵なメンバーを紹介するぞ！\w9\w9\uおー！！\w9\w9\h\s[6]\n\nミステリアスなお嬢さん、\w5ミスティーさん。\w9\nその兄でバイクに詳しい、\w5ルビーさん。\w9\n家庭的で謎めいたメイド、\w5エメラさん。\w9\w9\u\n\nうん。\w9\w9\h\s[3]\n\nでも一番謎めいている、\w5ゴーストフォルダヨンさん。\w9\w9\u\s[16]\n\nうん…\w5…\w5。'
        '\u\s[10]\h\s[0]ルビーさんはバイク。\w9\nエメラさんは軽トラック。\w9\s[3]\n…\w5…\w5ミスティーさんは？\w9\w9\u別に乗り物とペアって訳じゃないでしょ、\w5多分。'
    }
    --
    '\w9\w9\e'
}

// -------------------------------------------------------------------
// Adaptation
// -------------------------------------------------------------------
OnGhostChanged_悠梨
{
    OnBoot_talk_prelude
    --
    _temp = OnBoot_talk_priority
    if _temp != '' {
        _temp
        return
    }

    if 'ゆっくり' _in_ reference[1] {
        "\h\s[0]\u\s[10]悠梨さんと鈴さんからの交代よ。\w9\w9\h\s[5]%(username)をゆっくりさせよ、\w5と仰せつかった。\w9\nので、\w5頑張ってゆっくりさせよう。\w9\w9\u\n\nどういう？\w9\w9\h\s[6]\n\n人にはまずは手本を示せ、\w5と言います。\w9\w9\u\s[16]\n\nいつも通りじゃないの。"
    }
    else {
        // フォールバック
        '\u\s[10]\h\s[6]悠梨さんと鈴さんからの交代だ。\w9\w9\uありがとうございました。'
    }
}

OnGhostChanging_悠梨
{
    '\u\s[10]\h\s[0]悠梨さんと鈴さんは猫カフェをしているとのこと。\w9\s[6]\nちょっと行ってみたいな。\w9\w9\uもちろん付き添うわよ。\w9\w9\h\s[3]\n\n…\w5…\w5猫が猫カフェに行くと、\w5どうなるんだ？\w9\w9\u\n\nさぁ…\w5…\w5？'
}

// -------------------------------------------------------------------
// Other
// -------------------------------------------------------------------
OnGhostChangedOther
{
    OnBoot_talk_prelude
    --
    _temp = OnBoot_talk_priority
    if _temp != '' {
        _temp
        return
    }

    _s = SHIORI3FW.EscapeAllTags(reference[0])
    "\u\s[10]\h\s[6]%(_s)さんからの交代だ。\w9\w9\uありがとうございました。"
    --
    '\e'
}

OnGhostChangingOther
{
    _s = SHIORI3FW.EscapeAllTags(reference[0])
    "\u\s[10]\h\s[0]%(_s)さんへ交代だ。\w9\w9\n\n\u\s[10]よろしくお願いします。"
    --
    '\w9\w9\e'
}

// ******************************************************************************
// この下は切り替えトーク処理用
// 通常はいじる必要はありませんが、必要に応じて適宜変更してください。
// AYATEMPLATE.ではじまる関数は _tmpl_util.dic にあります。
// ******************************************************************************

OnGhostChanging
{
    On_Plugin_MailBox_sendMails
    --
    AYATEMPLATE.ExecuteChangeCallTalk("OnGhostChanging", "OnClose")
}

OnGhostChanged
{
    Plugin_MailBox_BootScript('OnGhostChangedEx')
}

OnGhostChangedEx
{
    AYATEMPLATE.ExecuteChangeCallTalk("OnGhostChanged", "OnBoot")
}

OnGhostCalling
{
    AYATEMPLATE.ExecuteChangeCallTalk("OnGhostCalling")
}

OnGhostCalled
{
    Plugin_MailBox_BootScript('OnGhostCalledEx')
}

OnGhostCalledEx
{
    AYATEMPLATE.ExecuteChangeCallTalk("OnGhostCalled", "OnBoot")
}

OnGhostCallComplete
{
    AYATEMPLATE.ExecuteChangeCallTalk("OnGhostCallComplete")
}

OnOtherGhostClosed
{
    AYATEMPLATE.ExecuteChangeCallTalk("OnOtherGhostClosed")
}

OnOtherGhostBooted
{
    AYATEMPLATE.ExecuteChangeCallTalk("OnOtherGhostBooted")
}

OnOtherGhostChanged
{
    AYATEMPLATE.ExecuteChangeCallTalk("OnOtherGhostChanged")
}
