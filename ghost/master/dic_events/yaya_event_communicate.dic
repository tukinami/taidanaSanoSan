// ******************************************************************************
// YAYA　ゴーストテンプレート
// コミュニケート辞書
// ******************************************************************************

// ******************************************************************************
// こちらから話しかける
// ******************************************************************************

// この関数は現状メニューから呼ばれます
StartCommunicate
{
    // 以下の関数は、隣に立っているゴーストを探して、
    // TalkTo_??? 関数を呼び出すものです。誰も居なければランダムトークをします。
    // yaya_tmpl_utilにあります。
    AYATEMPLATE.ExecuteTalkToTalk
}

// その他の人に話しかける
TalkToOther
{
    _target = SHIORI3FW.EscapeAllTags(res_reference0)
    "\u\s[10]\h\s[0]こんにちは、%(_target)さん。\1\s[10]お元気ですか？"
}

TalkToNobody
{
    "\u\s[10]\h\s[3]…\w5…\w5誰もいないな。\w9\w9\uあなたの後ろに…\w5…\w5\w9\w9\h\s[2]\n\nそういうのやめろ！"
}

// ******************************************************************************
// 話しかけられたので返事する
// ******************************************************************************

// ------------------------------------------------------------------------------
// OnCommunicateイベント
// ------------------------------------------------------------------------------

OnCommunicate
{
    if reference[2] == 'ECHO/1.0' {
        OnCommunicate_daihon
        return
    }
    // elseif reference[2] == 'JPRadish' {
    // }

    if reference[0] == 'user' || reference[0] == 'User' {
        ReplyToUser
    }
    else {
        AYATEMPLATE.ExecuteReplyToTalk(reference[0])
    }
}

// ------------------------------------------------------------------------------
// ユーザーからの話しかけ
// ------------------------------------------------------------------------------
ReplyToUser
{
    '\u\s[10]\h\s[3]ユーザーとのコミュニケートは「未実装」の筈だ…\w5…\w5。\w9\w9'
    --
    '\u何かしらの不具合なので、\w5作者に問い合わせてみて下さい。\w9'
    --
    '\s[16]\n…\w5…\w5とはいえ、\w9\s[11]「未実装」だなんて、\w5言っちゃっていいの？\w9\w9'
    --
    '\h\s[7]\n\nしょうがないだろ！\w9\s[4]他に何ていえばいいんだよ！'
}

// ------------------------------------------------------------------------------
// ゴーストからの話しかけ
// ------------------------------------------------------------------------------
ReplyToOther
{
    _str = SHIORI3FW.EscapeDangerousTags(reference[1])
    _greeting = ''
    _res = ""

    if RE_MATCH(_str, ".*(おはよう|オハヨウ|お早う).*") > 0 {
        _greeting = "おはようございます"
    }
    elseif RE_MATCH(_str, ".*(こんにち(は|わ)|(今日(は|わ|和))|(コンニチ(ワ|ハ))).*") > 0 {
        _greeting = "こんにちは"
    }
    elseif RE_MATCH(_str, ".*(こんちは|コンチワ).*") > 0 {
        _greeting = "こんにちは"
    }
    elseif RE_MATCH(_str, ".*(こんばん(は|わ)|(今晩(は|わ|和))|(コンバン(ワ|ハ))).*") > 0 {
        _greeting = "こんばんは"
    }
    elseif RE_MATCH(_str, ".*(ハロー|Hello|hello|HELLO|Ｈｅｌｌｏ|ｈｅｌｌｏ|ＨＥＬＬＯ).*") > 0 {
        _greeting = "こんにちは"
    }

    if RE_MATCH(_str, ".*(どうも).*") > 0 {
        // TODO
        // とりあえず汎用
        _greeting = "こんにちは"
    }

    if STRLEN(_greeting) != 0 {
        if communicate_ghost == SHIORI3FW.EscapeDangerousTags(reference[0]) {
            communicate_ghost = ''
            --
            "\u\s[10]\h\s[5]ご丁寧にどうも。\w9"
        }
        else {
            communicate_ghost = SHIORI3FW.EscapeDangerousTags(reference[0])
            --
            "\u\s[10]\h\s[5]%(_greeting)、\w5%(communicate_ghost)さん。\w9"
        }
    }
    else {
        "？\w9\n今何て？"
        return
    }
    res_reference[0] = reference[0]
}

ReplyTo_夢少女
{
    communicate_ghost = '夢少女'
    res_reference[0] = reference[0]
    _str = SHIORI3FW.EscapeDangerousTags(reference[1])
    --
    if '夢' _in_ _str {
        if '聞かせて' _in_ _str {
            '\u\s[10]\h\s[0]夢？\w9\s[6]\nそうだな、\w5最近見たのは、\w9\n'
            --
            '\h\s[31]深夜に夜食を買いに出たら、\w9\s[8]\nものすごく近所の筈なのに道に迷った夢、\w9\s[0]\nかな。\w9\w9\uあなたならやりかねない、\w5寝惚けて。'
            '\h\s[0]スーパーにたくあんを買いに行ったら、\w9\s[2]\n何故か軒並みたくあんが売り切れだった夢、\w9\s[0]\nかな。\w9\w9\u何故沢庵…\w5…\w5？'
            '\u\s[10]\h\s[5]パがつく音楽ゲームの新規続編が出て、\w5それをプレイする夢、\w9\s[0]\nかな。\w9\w9\uへぇ、\w5どんな感じだった？\w9\w9\h\s[2]\n\nニワトリだけ立体的になってた！\w9\w9\u\s[11]\n\nニワトリ？！'
        }
    }
}

// ===================================================================
// 台本コミュニケート
// ===================================================================
OnCommunicate_daihon
{
    _str = SHIORI3FW.EscapeDangerousTags(reference[3])
    res_reference0 = SHIORI3FW.EscapeDangerousTags(reference[0])
    _str
}
